apply plugin: 'maven-publish'

Properties properties = new Properties()
File propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    InputStream inputStream = propertiesFile.newDataInputStream()
    properties.load(inputStream)
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url 'https://maven.pkg.github.com/kuloud/HiveLocation'
                credentials {
                    username = properties.getProperty("gpr.user") ?: System.getenv("GPR_USER")
                    password = properties.getProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
                }
            }
        }
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                version releaseVersion
                groupId = 'com.kuloud.android'
                artifactId = releaseArtifact
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                version releaseVersion
                groupId = 'com.kuloud.android'
                artifactId = releaseArtifact + '-snapshot'
            }
        }
    }
}